group = 'host.exp.exponent'
version = '4.0.1'

apply from: './maven-eas.gradle'
if (findProperty('isAarProject')) { return }

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

buildscript {
  // Simple helper that allows the root project to override versions declared by this library.
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion', '1.6.10')}")
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 30)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 30)
    versionCode 12
    versionName '4.0.1'
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  implementation project(':expo-modules-core')

  implementation 'com.android.installreferrer:installreferrer:1.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${safeExtGet('kotlinVersion', '1.6.10')}"

  if (project.findProject(':unimodules-test-core')) {
    testImplementation project(':unimodules-test-core')
  }
  testImplementation "org.robolectric:robolectric:4.5.1"
  testImplementation 'junit:junit:4.12'
}
